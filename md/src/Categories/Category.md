
Why Category Theory?
===

**Q**: What do the following mathematical topics all have in common?

```hs
Sets             Graphs
Types            Groups
Vector Spaces    Automata
Formal Proofs    Computable Functions
```

**A**: They all have:
- Objects
- Transformations of objects into other objects
- Relationships with each other

Can we talk about all these branches of math using the same concepts, to mathematically define
what they have in common and how they are different?

Category Theory
===

A **Category** is a data structure with the following elements

- **Objects**, usually denoted `X`, `Y`, ...
- **Morphisms** `f : X ‚ü∂ Y`

And with the properties:

- **Identity**: For every `X`, `idX : X ‚ü∂ X` is a morphism
- **Composition**: If `f : X ‚ü∂ Y` and `g : Y ‚ü∂ Z` are arrows, then so is `f ‚â´ g : X ‚ü∂ Z`
- **Composition with Identites**: `idX ‚â´ f = f ‚â´ idX = f`.
- **Associativity**: `(f ‚â´ g) ‚â´ h = f ‚â´ (g ‚â´ h)`

Examples
===

**Sets**
  - Objects: Sets
  - Morphisms: Functions

**Vector Spaces**
  - Objects: Things like `‚Ñù‚Åø` with addition and scalar multiplication
  - Morphisms: Linear Transformations

**Contexts** (as in Lean's Info View)
  - Objects: Sets of assignments of variables to types
  - Moprhisms: Substitutions

Example: The Category of Directed Graphs
===

In Lean, a simple directed graph class is defined by. 
```lean
class Graph.{u} where
  V : Type u
  E : V ‚Üí V ‚Üí Prop

--hide
namespace Graph
--unhide
```


Here are two small graphs, for example.

<img src="https://docs.google.com/drawings/d/e/2PACX-1vQzWTtwjv7QALi-tC3RV_1lXZExyuMWckx4DGkuhzZu_9OSmD2ZldukzPPxVdtYwuJD3_tYLFh8gyrR/pub?w=695&amp;h=209" width=40% >

Let's show that the `Graph` type is a Category. 

Example: Graph Morphisms
===

Every category has a different notion of what a morphism is. For Graphs, a morphism
relating two graphs `G` and `H` is required to preserve the edge relationship.  
```lean
@[ext]
structure Hom (G H : Graph) where
  f : G.V ‚Üí H.V
  pe: ‚àÄ x y, G.E x y ‚Üí H.E (f x) (f y)
```

Example: Graph Quivers
===

With the notion of a `Graph` morphism defined, we can instantiate
the `quiver` class, which allows us to write `G ‚ü∂ H` for our morphisms. 
```lean
instance inst_quiver : Quiver Graph := ‚ü®
  fun G H => Hom G H
‚ü©

@[ext]
lemma Hom.ext_helper {G H : Graph} (f g : G ‚ü∂ H) (h : f.f = g.f) : f = g := by
  simp[Graph.inst_quiver] at f g
  cases f; cases g
  simp_all
```
 In Lean, the class `Category` extends the class `Quiver`.

Example: A Graph Morphism
===

As an example, here are two graphs:

<img src="https://docs.google.com/drawings/d/e/2PACX-1vQzWTtwjv7QALi-tC3RV_1lXZExyuMWckx4DGkuhzZu_9OSmD2ZldukzPPxVdtYwuJD3_tYLFh8gyrR/pub?w=695&amp;h=209" width=40%>

Suppose we have a function `f : V‚ÇÅ ‚Üí V‚ÇÇ` sending 0 ‚Ü¶ 0 and 0 ‚Ü¶ 1.
We check that `f` is a morphism:

  - 0 ‚Üí 1     ‚üπ     f(0) ‚Üí f(1)    ‚â°      0 ‚Üí 0       ‚úÖ
  - 1 ‚Üí 0     ‚üπ     f(1) ‚Üí f(0)    ‚â°      0 ‚Üí 0       ‚úÖ

Example: A Graph Morphism in Lean
===

```lean
def G : Graph := ‚ü® Fin 2, fun x y => x = y+1 ‚à® y = x+1 ‚ü©
def H : Graph := ‚ü® Fin 1, fun _ _ => True ‚ü©

def f : G ‚ü∂ H := ‚ü® fun v => ‚ü® 0, Nat.one_pos ‚ü©, by
    intro x y h
    simp_all[G,H]
  ‚ü©
```

Example: Identity and Composition
===

To instantiate Graph as a Category, we need and id morphism, and composition. 
```lean
def id_hom (G : Graph) : G ‚ü∂ G := ‚ü® fun x => x, fun _ _ h => h ‚ü©

def comp_hom {G H I : Graph} (œÜ : G ‚ü∂ H) (œà : H ‚ü∂ I) : G ‚ü∂ I :=
  ‚ü®
    œà.f ‚àò œÜ.f,
    by
      intro x y h
      exact œà.pe (œÜ.f x) (œÜ.f y) (œÜ.pe x y h)
  ‚ü©
```

Example: Graphs Form a Category
===

Showing graphs are a category is now easy. 
```lean
instance inst_category : Category Graph := {
  id := id_hom,
  comp := comp_hom,
  id_comp := by aesop,
  comp_id := by aesop,
  assoc := by aesop
}
```

Notation for Morphisms
===

The `Category` instances allows us to use the notation `ùüô G` and `‚â´`.

Note: `f ‚â´ g` means apply `f`, then apply `g`. It is notation for `comp g f`. 
```lean
example : (ùüô G) ‚â´ f = f := by rfl
example : f ‚â´ (ùüô H) = f := by rfl
example : ((ùüô G) ‚â´ f) ‚â´ (ùüô H) = (ùüô G) ‚â´ (f ‚â´ (ùüô H)) := by rfl

--hide
end Graph
--unhide
```

Isomorphisms
===

An isomorphism in a category `C` is a morphism `f : A ‚ü∂ B` such that there exists
a morphism `g : B ‚ü∂ A` with `f ‚â´ g = ùüô A`. In the category Graph, a simple example
is a function that relabels vertices.

```lean
def relabel (G : Graph) (r : G.V ‚âÉ G.V) : Graph := ‚ü®
  G.V,
  fun x y => G.E (r.symm x) (r.symm y)
‚ü©
```
 Here,
- `‚âÉ` means bijection
- `r.symm` is the inverse of r (technicly an `Equiv`).  

Example: The `relabel` isomorphism
===

We can define an Isomorphism from `relabel` as follows: 
```lean
def relabel_iso (G : Graph) (r : G.V ‚âÉ G.V)
  : Iso G (relabel G r) := ‚ü®
    ‚ü® r.toFun, by intro x y he
                  simp[relabel,he] ‚ü©,
    ‚ü® r.invFun, by intro x y he
                   exact he ‚ü©,
    by ext
       simp[Graph.inst_category,Graph.comp_hom,Graph.id_hom],
    by ext
       simp[Graph.inst_category,Graph.comp_hom,Graph.id_hom]
  ‚ü©
```

Functors
===

A ***Functor*** `F : C ‚•§ D` between categories `C` and `D` is a mapping of objects to
objects and morphisms to morphisms such that:

- If `f : A ‚ü∂ B` is a morphism in `C` then `F(f) : F(A) ‚ü∂ F(B)` is the resulting morphism in `D`.
- `F(ùüô A) = ùüô F(A)`
- `F(f ‚â´ g) = F(f) ‚â´ F(g)`



Example: The Forgetful Functor
===

The forgetful functor for a category is one that forgets everything except the type of its objects.
Here, we show the forgetful functor for `Graph`. Note that we use the category `Type` which is
instantiated as a Category in Mathlib. 
```lean
def V : Graph ‚•§ Type := {
  obj G := G.V,
  map {G H} f := f.f
}
```
 Note that the fields `map_id` and `map_comp` are discharged by the default proof
for the Functor class, which is `cat_disch`, so we don't need to prove them for simple cases.  

Example: The Id Endofunctor
===

```lean
def Graph.id : Graph ‚•§ Graph := {
  obj G := G,
  map {F G} f := f
}
```
 Note: Mathlib defines the identity functor for any category `C` by `ùü≠ C`, so
we didn't need to define `Graph.id` and could have written `ùü≠ Graph` instead. 

Example: Loop Addition
===

Adding all self loops to a graph is an endofunctor on Graph.


```lean
def Graph.add_loops : Graph ‚•§ Graph := {
  obj G := ‚ü® G.V, fun x y => x = x ‚à® G.E x x ‚ü©,
  map {F G} f := ‚ü® f.f, by simp ‚ü©
}
```

Example: Relabeling Functor
===

We can elevate relabeling to an endofunctor on Graph as follows.
We define a structure `VertexRelabeling` to contain the data
involved in the relabeling. The property `natural` requires
the relabeling `r` to be consistent across morphisms. 
```lean
structure VertexRelabeling where
  r : ‚àÄ G : Graph, G.V ‚âÉ G.V
  natural : ‚àÄ {G H : Graph} (f : G ‚ü∂ H),
            ‚àÄ x, (r H).symm (f.f x) = f.f ((r G).symm x)

def ReLabel (r : VertexRelabeling) : Graph ‚•§ Graph := {
  obj X := relabel X (r.r X),
  map {X Y} f := ‚ü® f.f, by
    intro x y he
    simp_all[relabel]
    rw[r.natural f y, r.natural f x]
    apply f.pe
    exact he
   ‚ü©
}
```

Natural Transformations
===

The requirement that graph relabeling be consistent in order to define a
Functor is a generic issue, which leads to the following definition.

Let `F : C ‚•§ D` and `G : C ‚•§ D` be morphisms. A ***natural transformation***
`Œ∑ : F ‚áí G` is a map between functors that
assigns every object `X : D` to a morphism `Œ∑_X : F(X) ‚ü∂ G(X)` such that

```lean
F.map f ‚â´ app Y = app X ‚â´ G.map f
```

<img src="https://wiki.haskell.org/wikiupload/e/ee/Natural_transformation.png" width=50%></img>



Natural Transormations in Lean
===

In Lean, natural transformations are defined via the structure:

```lean
structure NatTrans (F G : C ‚•§ D) where
  app : ‚àÄ X : C, F.obj X ‚ü∂ G.obj X
  naturality : ‚àÄ {X Y : C} (f : X ‚ü∂ Y),
    F.map f ‚â´ app Y = app X ‚â´ G.map f
```

Note that `C ‚•§ D` forms a category itself where the objects of the category
are morphisms from `C` to `D` and the morphisms are natural transformations.

Thus, to define a natural transformation you might not use `NatTrans` directly. You do:

```lean
def my_natural_transformation : C ‚ü∂ D := ...
```


Example: Relabeling is Natural
===

```lean
def relabel_nat (r : VertexRelabeling) : ùü≠ Graph ‚ü∂ (ReLabel r) := {
  app := fun G =>
    { f := r.r G,
      pe := by
        intros x y hxy
        dsimp[ReLabel,relabel]
        aesop
    },
  naturality := by -- Warning: AI Generated Proof
    intro X Y f
    ext x
    apply Equiv.injective (r.r Y)
    change (r.r Y) ((r.r Y ‚àò f.f) x) = (r.r Y) (f.f ((r.r X) x))
    dsimp [Functor.id, ReLabel, relabel, Functor.comp]
    aesop_cat_nonterminal
    apply (Equiv.apply_eq_iff_eq_symm_apply (r.r Y)).mpr
    simp[r.natural]
}

#check relabel_nat
```

Exercises
===

1. Define an inductive type `BinTree` to represent binary trees.
1. Show that `BinTree` forms a Category, using the same notion of morphism as we used for `Graph`.
1. Redo the above using Mathlib's `Fullsubcatgory` definition to define a new type `BinTree'`
   as a subcategory of `Graph`.


```lean
--hide
end LeanW26
--unhide
```

License
===

Copyright (C) 2025  Eric Klavins

This program is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.   

