
Category Theory: Exponentials
===

<img src="https://docs.google.com/drawings/d/e/2PACX-1vRE5Mmfx10f5A0c9oc94fXmYx0f5sEf4U-wh0c_esWBV02gyE0rMPcf1BBaZ5aoARFXBpSNp-S2uWh1/pub?w=1440&amp;h=1080" width=60%>

The Exponential Object Z^Y in many cases can be thought of all functions from
Y into Z. We need an evaluation function

```lean
  eval : Z^Y √ó Y ‚Üí Z
```
and a univeral property, which states that for any X and morphism
g : X √ó Y ‚Üí Z there is a unique morphism Œªg : X ‚Üí Z^Y so that (Œªg √ó (ùüô Y)) ‚â´ eval = g.



Currying
===

Currying (after the logician Haskell Curry, 1900-1982) is a way to take a function of two
arguments and combine it into two functions of one argument.

For example suppose `f = fun x y => x+y`. Then `f x = fun y => x+y`.

Here `f : X*Y ‚Üí Z` and `f x : Y ‚Üí Z`. The operation that does this is called `curry`,
which is an operation on functions:

```
    curry : (X*Y ‚Üí Z) ‚Üí (X ‚Üí (Y ‚Üí Z))
```

or in the notation of Category theory

```
    curry : (X*Y ‚ü∂ Z) ‚Üí (X ‚ü∂ Z^Y)
```

Currying in a Category
===

<img src="https://docs.google.com/drawings/d/e/2PACX-1vRE5Mmfx10f5A0c9oc94fXmYx0f5sEf4U-wh0c_esWBV02gyE0rMPcf1BBaZ5aoARFXBpSNp-S2uWh1/pub?w=1440&amp;h=1080" width=60%>

So `curry` to has type:

```lean
curry : (X √ó Y ‚ü∂ Z) ‚Üí (X ‚ü∂ Z^Y)
```

So

```lean
curry g : X ‚ü∂ Z^Y            and            Œªg = curry g √ó ùüô Y
```



HasExp
===

Here's the implementation

```lean
open HasProduct in
class HasExp.{u,v} (C : Type u) [Category.{v} C] [HasProduct.{u} C] where

  exp : C ‚Üí C ‚Üí C
  eval {Z Y : C} : (prod (exp Z Y) Y) ‚ü∂ Z
  curry {X Y Z : C} (g : (prod X Y) ‚ü∂ Z) : X ‚ü∂ (exp Z Y)

  curry_eval {X Y Z : C} (g : prod X Y ‚ü∂ Z)
    : ‚Äπcurry g, ùüô Y‚Ä∫ ‚â´ eval = g

  curry_unique {X Y Z : C} (g : X ‚ü∂ exp Z Y)
    : curry ( ‚Äπg, ùüô Y‚Ä∫ ‚â´ eval) = g
```

Notation Class Instances
===

```lean
instance HasExp.inst_hpow.{u, v} {C : Type u} [Category.{v} C]
         [HasProduct.{u} C] [HasExp.{u, v} C]
  : HPow C C C where
  hPow := exp

instance HasExp.inst_pow.{u, v} {C : Type u} [Category.{v} C]
         [HasProduct.{u} C] [HasExp.{u, v} C] : Pow C C where
  pow := exp
```
 Now we can write: 
```lean
universe u v
variable {C : Type u} [Category.{v} C] [HasProduct.{u, v} C] [HasExp.{u, v} C]
variable {X Y Z S A V : C}

#check (X^Y)*Z
```

Beta Reduction = `curry_eval`
===
Rewrite HasExp properties so they can be used with simp. 
```lean
open HasProduct HasExp in
@[simp, reassoc]
theorem beta {f : X * Y ‚ü∂ Z}
  : ‚Äπcurry f, ùüô Y‚Ä∫ ‚â´ eval = f := by
    apply curry_eval

open HasProduct HasExp in
@[simp, reassoc]
theorem beta_alt {f : X * Y ‚ü∂ Z}
  : (pair (œÄ‚ÇÅ ‚â´ curry f) (œÄ‚ÇÇ ‚â´ (ùüô Y))) ‚â´ eval = f := by
    apply curry_eval
```

Eta Reduction = `curry_unique`
===

```lean
open HasProduct HasExp in
@[simp, reassoc]
theorem eta {g : X ‚ü∂ exp Z Y}
  : curry ( ‚Äπg, ùüô Y‚Ä∫ ‚â´ eval ) = g := by
    apply curry_unique

open HasProduct HasExp in
@[simp, reassoc]
theorem eta_alt {g : X ‚ü∂ exp Z Y}
  : curry ( pair (œÄ‚ÇÅ ‚â´ g) (œÄ‚ÇÇ ‚â´ (ùüô Y)) ‚â´ eval ) = g := by
    apply curry_unique
```

Extensionality
===

These theorems state that if two functions evaluate to the same thing on their arguments,
then they are the same.

```lean
open HasProduct HasExp in
lemma exp_ext {f g : X ‚ü∂ (Z ^ Y)} :
  ‚Äπf,  ùüô Y‚Ä∫ ‚â´ eval = ‚Äπg,  ùüô Y‚Ä∫ ‚â´ eval ‚Üí f = g := by
    intro hyp
    apply congrArg curry at hyp
    rw[eta,eta] at hyp
    exact hyp

open HasProduct HasExp in
lemma exp_ext_alt {f g : X ‚ü∂ (Z ^ Y)} :
  pair (œÄ‚ÇÅ ‚â´ f) (œÄ‚ÇÇ ‚â´ ùüô Y) ‚â´ eval = pair (œÄ‚ÇÅ ‚â´ g) (œÄ‚ÇÇ ‚â´ ùüô Y) ‚â´ eval ‚Üí f = g := by
    intro hyp
    apply congrArg curry at hyp
    rw[eta_alt,eta_alt] at hyp
    exact hyp
```

Uncurrying
===

```lean
def HasExp.uncurry (g : X ‚ü∂ Z ^ Y) : X * Y ‚ü∂ Z :=   ‚Äπg, ùüô Y‚Ä∫  ‚â´ HasExp.eval

open HasProduct HasExp in
theorem curry_uncurry (g : X * Y ‚ü∂ Z)
  : uncurry (curry g) = g := by apply curry_eval

open HasProduct HasExp in
theorem uncurry_curry (g : X ‚ü∂ Z ^ Y)
  : curry (uncurry g) = g := by apply curry_unique

open HasProduct HasExp in
theorem uncurry_both_sides {f g : X ‚ü∂ Z ^ Y}
  : f = g ‚Üî (uncurry f = uncurry g) := by
  constructor
  ¬∑ intro h
    rw[h]
  ¬∑ intro h
    apply congrArg curry at h
    simp[uncurry_curry] at h
    exact h
```

Sliding
===

```lean
open HasProduct HasExp in
@[simp, reassoc]
theorem sliding {m : S ‚ü∂ A} {n : X ‚ü∂ V} {h : A * V ‚ü∂ X}
    : ‚Äπm ‚â´ curry h, n‚Ä∫ ‚â´ eval =  ‚Äπm, n‚Ä∫ ‚â´ h := by
    have : n = n ‚â´ (ùüô V) := Eq.symm (Category.comp_id n)
    rw[this]
    rw[prod_map_comp]
    rw[Category.assoc]
    rw[‚Üêthis]
    have : ‚Äπcurry h, ùüô V‚Ä∫ ‚â´ HasExp.eval = h := by
      apply curry_eval
    rw[this]
```

Associativity
===

```lean
open HasProduct HasExp in
theorem exp_prod : ‚àÉ f : Iso ((X^Y)^Z) (X^(Y*Z)), True := by

    let F : ((X^Y)^Z)*(Y*Z) ‚ü∂ X :=
        pair (‚Äπ ùüô ((X ^ Y) ^ Z), œÄ‚ÇÇ ‚Ä∫ ‚â´ eval) (œÄ‚ÇÇ ‚â´ œÄ‚ÇÅ) ‚â´ eval

    let f : (X^Y)^Z ‚ü∂ X^(Y*Z) :=
        curry F

    let G : (X^(Y*Z)*Z)*Y ‚ü∂ X :=
        pair (œÄ‚ÇÅ ‚â´ œÄ‚ÇÅ) (pair œÄ‚ÇÇ (œÄ‚ÇÅ ‚â´ œÄ‚ÇÇ)) ‚â´ eval

    let g : X^(Y*Z) ‚ü∂ (X^Y)^Z :=
        curry (curry G)

    use ‚ü®
      f,
      g,
      by
        simp[f,g]

        sorry,
      by
        simp[f,g]
        sorry
    ‚ü©
```

Example: Reflexive Graphs: A Subcategory of Graphs
===

To show an example of exponentials, we can't use simple graphs, as we need self-loops (Why?)
We can build a subcategory of Graph called ReflexiveGraph that does this using
Mathlib's `FullSubcategory` helper. 
```lean
def ReflexiveGraph : Type (u+1) :=
  ObjectProperty.FullSubcategory (fun G : Graph.{u} => ‚àÄ v, G.E v v)

--hide
namespace ReflexiveGraph
--unhide
```
 We can then show ReflexiveGraph is also a category and that it has products. 
```lean
instance inst_category : Category ReflexiveGraph.{u} :=
  ObjectProperty.FullSubcategory.category _
```

Products in Reflexive Graphs
===

For the product instance, it would be nice if there were a way to just use the
fact that Graphs have products. Or at least use some of that proof. But I could not
figure that out so this is mostly just repetetive at this point. 
```lean
open HasProduct in
instance inst_has_product : HasProduct.{u+1} ReflexiveGraph.{u} := {

  prod := fun G H => ‚ü® TensorProd G.1 H.1, fun v => ‚ü® G.property v.1, H.property v.2 ‚ü© ‚ü©,
  œÄ‚ÇÅ := fun {X‚ÇÅ X‚ÇÇ : ReflexiveGraph} => Graph.inst_has_product.œÄ‚ÇÅ,
  œÄ‚ÇÇ := fun {X‚ÇÅ X‚ÇÇ : ReflexiveGraph} => Graph.inst_has_product.œÄ‚ÇÇ,

  pair := fun {X‚ÇÅ X‚ÇÇ Y : ReflexiveGraph} => fun f‚ÇÅ f‚ÇÇ => ‚ü® fun y => ( f‚ÇÅ.f y, f‚ÇÇ.f y ), by
    intro x y h
    exact ‚ü® f‚ÇÅ.pe x y h, f‚ÇÇ.pe x y h ‚ü©
  ‚ü©,

  pair‚ÇÅ := by intros; rfl,

  pair‚ÇÇ := by intros; rfl,

  pair_unique {X‚ÇÅ X‚ÇÇ Y} := by
    intro h
    rfl

}
```

Reflexive Graphs Have Exponentials
===

Here's a paper with something like this construction:

  https://arxiv.org/pdf/1807.09345

The exponential G^H is the graph in which the vertices are morphisms from H to G,
and the edges are the "natural transformations". The transformation maps vertices
and edges of G to those of H in a way that commutes.

HasExp Instantiation
===

First we define the exponential object of two Reflexive Graphs. Note the universe
condition. We need to have the exponential object like in a higher universe
otherwise we get Russel's paradox issues.

The vertices of the exponential objects G^H are the morphisms from H to G.

If F‚ÇÅ and F‚ÇÇ are both morphisms from H to G, there is an edge from
F‚ÇÅ to F‚ÇÇ if for all edges x y in H there is an edge from F‚ÇÅ(x) to F‚ÇÇ(x) in G.


```lean
def exp (G H : ReflexiveGraph.{u}) : ReflexiveGraph.{u} := {
  obj := {
    V := ULift.{u} (H ‚ü∂ G),
    E := fun F‚ÇÅ F‚ÇÇ =>
      let ‚ü® f‚ÇÅ, _ ‚ü© := ULift.down F‚ÇÅ -- function underlying first morphism
      let ‚ü® f‚ÇÇ, _ ‚ü© := ULift.down F‚ÇÇ -- function underlying second morphism
      let ‚ü® ‚ü® hV, hE ‚ü©, _ ‚ü© := H     -- vertices and edges of H
      let ‚ü® ‚ü® _, gE ‚ü©, _ ‚ü© := G      -- edges of G
      ‚àÄ x y : hV, hE x y ‚Üí gE (f‚ÇÅ x) (f‚ÇÇ y)
  },
  property := by
    intro morphism u v h
    exact morphism.down.pe u v h
}
```

The eval Function is straighforward
===

```lean
def eval (H G : ReflexiveGraph) : HasProduct.prod (exp H G) G ‚ü∂ H := {
    f := fun ‚ü® ‚ü® f, h ‚ü©, v  ‚ü© => f v,
    pe := by
      intro ‚ü® ‚ü® fg, hfg ‚ü©, vG ‚ü© ‚ü® ‚ü® fh, hfh ‚ü©, fH ‚ü© ‚ü® h1, h2 ‚ü©
      simp_all only[exp]
      exact h1 vG fH h2
}
```

Instantiating HasExp
===

```lean
instance inst_has_exp : HasExp ReflexiveGraph := {

  exp := exp,
  eval := fun {G H} => eval G H,

  curry := fun {X Y Z} => fun ‚ü® f, h ‚ü© => ‚ü® fun x => ‚ü® fun y => f (x,y), by
      intro _ _ e
      apply h
      exact ‚ü® X.property x, e ‚ü©
     ‚ü©, by
        intros _ _ ex _ _ ey
        apply h
        exact ‚ü®ex, ey‚ü©
    ‚ü©

  curry_eval := by intros; rfl,

  curry_unique := by
    intro X Y A g
    rfl

}
```
 Hooray! 
```lean
--hide
end ReflexiveGraph
--unhide

--hide
end LeanW26
--unhide
```

License
===

Copyright (C) 2025  Eric Klavins

This program is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.   

