
Category Theory: Binary Products
===

A `binary product` of two objects `Xâ‚` and `Xâ‚‚` in a category is an object called `Xâ‚ Ã— Xâ‚‚`.

A `projection` of a binary product throws away one of the parts:

```lean
   Ï€â‚ (Xâ‚ Ã— Xâ‚‚) = Xâ‚
   Ï€â‚‚ (Xâ‚ Ã— Xâ‚‚) = Xâ‚‚
```

<img src="https://docs.google.com/drawings/d/e/2PACX-1vRcGx-5-JPZkvvFdkf8-u-L67BcyFh-GzLcfgk4NBjPaLivE2nSPQIdrbg5y4AQMIysqqMWeXd3kg1y/pub?w=576&amp;h=315" width=40%>

Universal Property for Binary Products
===

> For every object `Y` and morphisms `fâ‚ : Y âŸ¶ Xâ‚`
> and `fâ‚‚ : Y âŸ¶ Xâ‚‚` there is a unique morphism `f : Y âŸ¶ Xâ‚ Ã— Xâ‚‚` such that
> `f â‰« Ï€â‚ = fâ‚` and `f â‰« Ï€â‚‚ = fâ‚‚`.

<img src="https://docs.google.com/drawings/d/e/2PACX-1vQPk2cl9FCCrOcGcwbIJtqL_-lP-d20u6wWSJEZhAsc6EwopVkNBU2sjAmJJZwkj7nXZb8RU4cQoc4H/pub?w=960&amp;h=720" width=60%>


The `pair` function
===

We call the unique morphism in the universal property for binary products `pair`. In Lean
it has type

```lean
pair {Xâ‚ Xâ‚‚ Y : C} (_ : Y âŸ¶ Xâ‚) (_ : Y âŸ¶ Xâ‚‚) : Y âŸ¶ (prod Xâ‚ Xâ‚‚)
```

Binary Products in Lean
===

The properties `pairáµ¢` record the universal property, and the `unique_pair`
property records the requirement the morphism is unique. 
```lean
@[ext]
class HasProduct.{u,v} (C : Type u) [Category.{v} C] where

  prod : C â†’ C â†’ C
  Ï€â‚ {Xâ‚ Xâ‚‚ : C} : (prod Xâ‚ Xâ‚‚) âŸ¶ Xâ‚
  Ï€â‚‚ {Xâ‚ Xâ‚‚ : C} : (prod Xâ‚ Xâ‚‚) âŸ¶ Xâ‚‚
  pair {Xâ‚ Xâ‚‚ Y : C} (_ : Y âŸ¶ Xâ‚) (_ : Y âŸ¶ Xâ‚‚) : Y âŸ¶ (prod Xâ‚ Xâ‚‚)

  pairâ‚ {Xâ‚ Xâ‚‚ Y : C} (fâ‚ : Y âŸ¶ Xâ‚) (fâ‚‚ : Y âŸ¶ Xâ‚‚) : pair fâ‚ fâ‚‚ â‰« Ï€â‚ = fâ‚
  pairâ‚‚ {Xâ‚ Xâ‚‚ Y : C} (fâ‚ : Y âŸ¶ Xâ‚) (fâ‚‚ : Y âŸ¶ Xâ‚‚) : pair fâ‚ fâ‚‚ â‰« Ï€â‚‚ = fâ‚‚
  pair_unique {Xâ‚ Xâ‚‚ Y : C} {h : Y âŸ¶ prod Xâ‚ Xâ‚‚}
    : h = pair (h â‰« Ï€â‚) (h â‰« Ï€â‚‚)

attribute [simp, reassoc] HasProduct.pairâ‚ HasProduct.pairâ‚‚

--hide
namespace HasProduct
--unhide
```

Product Notation
===

Instead of writing `prod A B` we would rather write `A * B`. So we instantiate the notation
classes for `*`:

```lean
instance inst_hmul {C : Type*} [Category C] [HasProduct C] : HMul C C C where
  hMul := prod

instance inst_mul {C : Type*} [Category C] [HasProduct C] : Mul C where
  mul := prod
```
 For example 
```lean
example {C : Type*} [Category C] [HasProduct C] (A B : C) : A*B = A*B := by rfl
```


Annoyingly, there does not seem to be a notation class for Ã— in Mathlib, perhaps
because the powers that be want to use that symbol exlusively for cartesian products
of types.

Simplifications
===

You can't label a class property with `@[simp]`, so we restate them as theorems.

```lean
universe u v
variable {C : Type u} [Category.{v} C] [HasProduct C] {A B U V W X Xâ‚ Xâ‚‚ Y Yâ‚ Yâ‚‚ Z : C}

@[simp]
theorem pair_simp_1 {fâ‚ : Y âŸ¶ Xâ‚} {fâ‚‚ : Y âŸ¶ Xâ‚‚} : pair fâ‚ fâ‚‚ â‰« Ï€â‚ = fâ‚ := by
  exact pairâ‚ fâ‚ fâ‚‚

@[simp]
theorem pair_simp_2 {fâ‚ : Y âŸ¶ Xâ‚} {fâ‚‚ : Y âŸ¶ Xâ‚‚} : pair fâ‚ fâ‚‚ â‰« Ï€â‚‚ = fâ‚‚ := by
  exact pairâ‚‚ fâ‚ fâ‚‚

theorem pair_unique_simp {h : Y âŸ¶ prod Xâ‚ Xâ‚‚} : h = pair (h â‰« Ï€â‚) (h â‰« Ï€â‚‚) := by
  exact pair_unique

theorem pair_unique_simp_2 {fâ‚ : Y âŸ¶ Xâ‚} {fâ‚‚ : Y âŸ¶ Xâ‚‚} {h : Y âŸ¶ prod Xâ‚ Xâ‚‚}
    {hâ‚ : h â‰« Ï€â‚ = fâ‚} {hâ‚‚ : h â‰« Ï€â‚‚ = fâ‚‚} : h = pair fâ‚ fâ‚‚ := by
    rw[â†hâ‚,â†hâ‚‚]
    exact pair_unique
```

Pairs of Projections
===

This theorem states that when you take a pair of projections, you
get the identity map.

<!-- https://q.uiver.app/#q=WzAsMyxbMSwwLCJYKlkiXSxbMiwwLCJZIl0sWzAsMCwiWCJdLFswLDIsIlxccGlfMSIsMl0sWzAsMSwiXFxwaV8yIl0sWzAsMCwiMV97WCpZfSJdXQ== -->
<iframe class="quiver-embed" src="https://q.uiver.app/#q=WzAsMyxbMSwwLCJYKlkiXSxbMiwwLCJZIl0sWzAsMCwiWCJdLFswLDIsIlxccGlfMSIsMl0sWzAsMSwiXFxwaV8yIl0sWzAsMCwiMV97WCpZfSJdXQ==&embed" width="351" height="220" style="border-radius: 8px; border: none;"></iframe>


```lean
@[simp, reassoc]
theorem pair_id : pair (Ï€â‚ : X*Y âŸ¶ X) (Ï€â‚‚ : X*Y âŸ¶ Y) = ğŸ™ (X*Y) := by
    apply Eq.symm
    rw[â†Category.id_comp Ï€â‚, â†Category.id_comp Ï€â‚‚]
    apply pair_unique
```

Conditions for a map to be the Identity
===

The next theorem describes when `f : X * Y âŸ¶ X * Y` is the identity on
`X * Y`.

```lean
@[simp]
lemma prod_id_unique {f : X * Y âŸ¶ X * Y} {hâ‚ : f â‰« Ï€â‚ = Ï€â‚} {hâ‚‚ : f â‰« Ï€â‚‚ = Ï€â‚‚}
  : f = ğŸ™ (X*Y) := by
    rw[â†pair_id,â†hâ‚,â†hâ‚‚]
    apply pair_unique
```

Composing Pairs
===

This theorem shows how to compose pairs.

```lean
@[simp, reassoc]
lemma comp_pair {h : W âŸ¶ X} {f : X âŸ¶ Y} {g : X âŸ¶ Z} :
  h â‰« pair f g = pair (h â‰« f) (h â‰« g) := by
  rw[pair_unique (h := h â‰« pair f g )]
  simp
```

Associativity Diagram
===

<table><tr>

<td>
<!-- https://q.uiver.app/#q=WzAsNyxbMSwwLCIoWCpZKSpaIl0sWzAsMSwiWCpZIl0sWzIsMSwiWiJdLFsxLDIsIlkiXSxbMCwzLCJYIl0sWzIsMywiWSpaIl0sWzEsNCwiWCooWSpaKSJdLFswLDEsIlxccGlfMSIsMl0sWzAsMiwiXFxwaV8yIl0sWzEsNCwiXFxwaV8xIiwyXSxbMSwzLCJcXHBpXzIiXSxbNSwzLCJcXHBpXzEiXSxbNiw1LCJcXHBpXzIiXSxbNiw0LCJcXHBpXzEiXSxbNSwyLCJcXHBpXzIiLDJdXQ== -->
<iframe class="quiver-embed" src="https://q.uiver.app/#q=WzAsNyxbMSwwLCIoWCpZKSpaIl0sWzAsMSwiWCpZIl0sWzIsMSwiWiJdLFsxLDIsIlkiXSxbMCwzLCJYIl0sWzIsMywiWSpaIl0sWzEsNCwiWCooWSpaKSJdLFswLDEsIlxccGlfMSIsMl0sWzAsMiwiXFxwaV8yIl0sWzEsNCwiXFxwaV8xIiwyXSxbMSwzLCJcXHBpXzIiXSxbNSwzLCJcXHBpXzEiXSxbNiw1LCJcXHBpXzIiXSxbNiw0LCJcXHBpXzEiXSxbNSwyLCJcXHBpXzIiLDJdXQ==&embed" width="300" height="350" style="border-radius: 8px; border: none;"></iframe>
</td>

<td>
Ï€â‚ â‰« Ï€â‚‚ : (XÃ—Y)Ã—Z âŸ¶ Y<br>
Ï€â‚‚ : (XÃ—Y)Ã—Z âŸ¶ Z<br>
Ï€â‚ â‰« Ï€â‚ : (XÃ—Y)Ã—Z âŸ¶ X<br>
âŸ¹<br>
pair (Ï€â‚ â‰« Ï€â‚‚) Ï€â‚‚ : (XÃ—Y)Ã—Z âŸ¶ YÃ—Z<br>
pair (Ï€â‚ â‰« Ï€â‚) (pair (Ï€â‚ â‰« Ï€â‚‚) Ï€â‚‚) : (XÃ—Y)Ã—Z  âŸ¶ XÃ—(YÃ—Z)<br>
<br>
Similarly,<br>
pair (pair Ï€â‚ (Ï€â‚‚ â‰« Ï€â‚)) (Ï€â‚‚ â‰« Ï€â‚‚) : XÃ—(YÃ—Z) âŸ¶ (XÃ—Y)Ã—Z<br>
</td>
</tr></table>

Proof of Associativity
===


```lean
@[simp]
def prod_assoc : (X*Y)*Z â‰… X*(Y*Z) :=
    {
      hom := pair (Ï€â‚ â‰« Ï€â‚) (pair (Ï€â‚ â‰« Ï€â‚‚) Ï€â‚‚),
      inv := pair (pair Ï€â‚ (Ï€â‚‚ â‰« Ï€â‚)) (Ï€â‚‚ â‰« Ï€â‚‚),
      hom_inv_id := by
        apply prod_id_unique
        Â· simp[â†Category.assoc]
          rw[â†pair_unique_simp]
        Â· simp[â†Category.assoc],
      inv_hom_id := by
         apply prod_id_unique
         Â· simp[â†Category.assoc]
         Â· simp[â†Category.assoc]
           rw[â†pair_unique_simp]
    }
```

A Condition for Equality of two Morphisms
===

```lean
theorem hom_ext {A B : C} {f g : X âŸ¶ A * B}
  : f = g â†” f â‰« Ï€â‚ = g â‰« Ï€â‚ âˆ§ f â‰« Ï€â‚‚ = g â‰« Ï€â‚‚ := by
  constructor
  Â· intro h
    constructor
    Â· exact congrFun (congrArg CategoryStruct.comp h) Ï€â‚
    Â· exact congrFun (congrArg CategoryStruct.comp h) Ï€â‚‚
  Â· intro âŸ¨ h1, h2 âŸ©
    rw[pair_unique (h := f)]
    rw[pair_unique (h := g)]
    rw[h1,h2]
```

Pairs of Morphisms
===

Pair only describes how to take the product of morphisms with the same domain.
The following method, which builds on `pair`, allows products of arbitary morphisms,
which will be useful in defining exponentials later.  
```lean
def prod_map {Xâ‚ Yâ‚ Xâ‚‚ Yâ‚‚ : C} (fâ‚ : Yâ‚ âŸ¶ Xâ‚) (fâ‚‚ : Yâ‚‚ âŸ¶ Xâ‚‚)
  : Yâ‚ * Yâ‚‚ âŸ¶ Xâ‚ * Xâ‚‚ := pair (Ï€â‚ â‰« fâ‚) (Ï€â‚‚ â‰« fâ‚‚)
```

Notation for Pairs of Morphisms
===

When `f` and `g` are morphisms, we want to write `f*g` for their prodict, so
we instantiate the notation class for `*` for morphisms as well.

  â€¹ is typed \f<
  â€º is typed \f>


```lean
notation "â€¹" fâ‚ ", " fâ‚‚ "â€º" => prod_map fâ‚ fâ‚‚

namespace Temp

variable (C : Type*) [Category C] [HasProduct C] (X Y : C) (f g : X âŸ¶ Y)
#check â€¹f,gâ€º
#check â€¹ Ï€â‚ â‰« f, g â‰« ğŸ™ Y â€º

end Temp
```

Simplifiers for Products
===

```lean
@[simp]
theorem prod_to_pair {fâ‚ : Yâ‚ âŸ¶ Xâ‚} {fâ‚‚ : Yâ‚‚ âŸ¶ Xâ‚‚}
   : â€¹ fâ‚, fâ‚‚ â€º = pair (Ï€â‚ â‰« fâ‚) (Ï€â‚‚ â‰« fâ‚‚) := by rfl

@[simp]
theorem prod_notation_to_pair {fâ‚ : Yâ‚ âŸ¶ Xâ‚} {fâ‚‚ : Yâ‚‚ âŸ¶ Xâ‚‚}
   : â€¹ fâ‚, fâ‚‚ â€º = pair (Ï€â‚ â‰« fâ‚) (Ï€â‚‚ â‰« fâ‚‚) := by rfl
```

Theorems About Morphism Products
===

```lean
@[simp]
lemma prod_map_comp {fâ‚ : X âŸ¶ Y} {fâ‚‚ : Y âŸ¶ Z} {gâ‚ : U âŸ¶ V} {gâ‚‚ : V âŸ¶ W}
  : â€¹ fâ‚ â‰« fâ‚‚, gâ‚ â‰« gâ‚‚ â€º = â€¹ fâ‚, gâ‚ â€º â‰« â€¹fâ‚‚, gâ‚‚â€º := by simp[â†Category.assoc]

@[simp]
lemma prod_map_spec {Xâ‚ Yâ‚ Xâ‚‚ Yâ‚‚ : C} {fâ‚ : Yâ‚ âŸ¶ Xâ‚} {fâ‚‚ : Yâ‚‚ âŸ¶ Xâ‚‚} :
  â€¹ fâ‚, fâ‚‚ â€º â‰« Ï€â‚ = Ï€â‚ â‰« fâ‚ âˆ§ â€¹ fâ‚, fâ‚‚ â€º â‰« Ï€â‚‚ = Ï€â‚‚ â‰« fâ‚‚ := by simp

lemma prod_map_unique {Z Xâ‚ Xâ‚‚ : C} {gâ‚ : Z âŸ¶ Xâ‚} {gâ‚‚ : Z âŸ¶ Xâ‚‚}
  {h : Z âŸ¶ prod Xâ‚ Xâ‚‚} {hâ‚ : h â‰« Ï€â‚ = gâ‚} {hâ‚‚ : h â‰« Ï€â‚‚ = gâ‚‚} :
  h = pair gâ‚ gâ‚‚ := by
    rw[â†hâ‚,â†hâ‚‚]
    exact pair_unique
```

More Theorems About Morphism Products
===

```lean
theorem prod_univ {Xâ‚ Xâ‚‚ Yâ‚ Yâ‚‚ : C} (fâ‚ : Yâ‚ âŸ¶ Xâ‚) (fâ‚‚ : Yâ‚‚ âŸ¶ Xâ‚‚) :
  â€¹ fâ‚, fâ‚‚ â€º â‰« Ï€â‚ = Ï€â‚ â‰« fâ‚ âˆ§ â€¹ fâ‚, fâ‚‚ â€º â‰« Ï€â‚‚ = Ï€â‚‚ â‰« fâ‚‚ := by simp

lemma prod_map_fst {f : A âŸ¶ X} {g : B âŸ¶ Y} :
    â€¹f,gâ€º â‰« Ï€â‚ = Ï€â‚ â‰« f := by simp

lemma prod_map_snd {f : A âŸ¶ X} {g : B âŸ¶ Y} :
    â€¹f,gâ€º â‰« Ï€â‚‚ = Ï€â‚‚ â‰« g := by simp

lemma fst_fst : (Ï€â‚ : (X*Y)*Z âŸ¶ X*Y) â‰« (Ï€â‚ : X*Y âŸ¶ X) = prod_assoc.hom â‰« Ï€â‚ := by simp

lemma snd_snd : (Ï€â‚‚ : X*(Y*Z) âŸ¶ Y*Z) â‰« Ï€â‚‚ = prod_assoc.inv â‰« Ï€â‚‚ := by simp
```

Example: Graphs Have Products
===

Graphs have products called Tensor Products, which we can use to instantiate the `HasProduct` class.

<img src="https://docs.google.com/drawings/d/e/2PACX-1vS8m1ASMsZn0P7p6k0rOGj-8KKBhahoNL7SvrASBquIOwZdxX3_t_49JfFJ7WtowCD-AvSfSe1vkldt/pub?w=814&amp;h=368" width=50% \>


```lean
def TensorProd (G H : Graph) : Graph := {
  V := G.V Ã— H.V,
  E := fun (u1,v1) (u2,v2) => G.E u1 u2 âˆ§ H.E v1 v2
}

--hide
namespace TensorProd
--unhide
```

Example: Tensor Product Properties
===

To form an instance of a `HasProduct` It will be convenient to have the following
properties defined as theorems, which state that products preserve edges.


```lean
theorem left {G H : Graph} :
  âˆ€ x y, (TensorProd G H).E x y â†’ G.E x.1 y.1 := by
  intro x y h
  exact h.left

theorem right {G H : Graph} :
  âˆ€ x y, (TensorProd G H).E x y â†’ H.E x.2 y.2 := by
  intro x y h
  exact h.right

--hide
end TensorProd
--unhide
```

Example: Graphs Have Products
===

Now we can instantiate the `HasProduct` class for graphs. 
```lean
instance Graph.inst_has_product : HasProduct Graph := {
  prod := TensorProd,
  Ï€â‚ := fun {Xâ‚ Xâ‚‚ : Graph} => âŸ¨ Prod.fst, TensorProd.left âŸ©,
  Ï€â‚‚ := fun {Xâ‚ Xâ‚‚ : Graph} => âŸ¨ Prod.snd, TensorProd.rightâŸ©,
  pair := fun {X Y Z} fâ‚ fâ‚‚ => âŸ¨ fun z => ( fâ‚.f z, fâ‚‚.f z ), by
      intro x y h
      exact âŸ¨ fâ‚.pe x y h, fâ‚‚.pe x y h âŸ©
    âŸ©
  pairâ‚ := by intros; rfl
  pairâ‚‚ := by intros; rfl
  pair_unique := by
    intro Xâ‚ Xâ‚‚ Y h
    exact rfl
}

--hide
end HasProduct
end LeanW26
--unhide
```

License
===

Copyright (C) 2025  Eric Klavins

This program is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.   

