
Category Theory: Binary Products
===

A ***binary product*** of two objects `Xâ‚` and `Xâ‚‚` in a category is an object called `Xâ‚ Ã— Xâ‚‚`.

A ***projection*** of a binary product throws away one of the parts:

```lean
   Ï€â‚ (Xâ‚ Ã— Xâ‚‚) = Xâ‚
   Ï€â‚‚ (Xâ‚ Ã— Xâ‚‚) = Xâ‚‚
```

<img src="https://docs.google.com/drawings/d/e/2PACX-1vRcGx-5-JPZkvvFdkf8-u-L67BcyFh-GzLcfgk4NBjPaLivE2nSPQIdrbg5y4AQMIysqqMWeXd3kg1y/pub?w=576&amp;h=315"
     width=30%>

Universal Property for Binary Products
===

For every object `Y` and morphisms `fâ‚ : Y âŸ¶ Xâ‚`
and `fâ‚‚ : Y âŸ¶ Xâ‚‚` there is a unique morphism `f : Y âŸ¶ Xâ‚ Ã— Xâ‚‚` such that
`f â‰« Ï€â‚ = fâ‚` and `f â‰« Ï€â‚‚ = fâ‚‚`.

<img src="https://docs.google.com/drawings/d/e/2PACX-1vQPk2cl9FCCrOcGcwbIJtqL_-lP-d20u6wWSJEZhAsc6EwopVkNBU2sjAmJJZwkj7nXZb8RU4cQoc4H/pub?w=960&amp;h=720"
     width=40%>

We call the unique morphism in the universal property for binary products `pair`. In Lean
it has type

```lean
pair {Xâ‚ Xâ‚‚ Y : C} (_ : Y âŸ¶ Xâ‚) (_ : Y âŸ¶ Xâ‚‚) : Y âŸ¶ (prod Xâ‚ Xâ‚‚)
```

Binary Products in Lean
===

The properties `pairáµ¢` record the universal property, and the `unique_pair`
property records the requirement the morphism is unique. 
```lean
@[ext]
class HasProduct.{u,v} (C : Type u) [Category.{v} C] where

  prod : C â†’ C â†’ C
  Ï€â‚ {Xâ‚ Xâ‚‚ : C} : (prod Xâ‚ Xâ‚‚) âŸ¶ Xâ‚
  Ï€â‚‚ {Xâ‚ Xâ‚‚ : C} : (prod Xâ‚ Xâ‚‚) âŸ¶ Xâ‚‚
  pair {Xâ‚ Xâ‚‚ Y : C} (_ : Y âŸ¶ Xâ‚) (_ : Y âŸ¶ Xâ‚‚) : Y âŸ¶ (prod Xâ‚ Xâ‚‚)

  pairâ‚ {Xâ‚ Xâ‚‚ Y : C} (fâ‚ : Y âŸ¶ Xâ‚) (fâ‚‚ : Y âŸ¶ Xâ‚‚)
    : pair fâ‚ fâ‚‚ â‰« Ï€â‚ = fâ‚ := by aesop_cat
  pairâ‚‚ {Xâ‚ Xâ‚‚ Y : C} (fâ‚ : Y âŸ¶ Xâ‚) (fâ‚‚ : Y âŸ¶ Xâ‚‚)
    : pair fâ‚ fâ‚‚ â‰« Ï€â‚‚ = fâ‚‚ := by aesop_cat
  pair_unique {Xâ‚ Xâ‚‚ Y : C} {h : Y âŸ¶ prod Xâ‚ Xâ‚‚}
    : h = pair (h â‰« Ï€â‚) (h â‰« Ï€â‚‚) := by aesop_cat

--hide
attribute [simp, reassoc] HasProduct.pairâ‚ HasProduct.pairâ‚‚
namespace HasProduct

universe u v
variable {C : Type u} [Category.{v} C] [HasProduct C] {A B U V W W' X Xâ‚ Xâ‚‚ Y Yâ‚ Yâ‚‚ Z : C}

--unhide
```

Product Notation
===

Instead of writing `prod A B` we would rather write `A * B`. So we instantiate the notation
classes for `*`:

```lean
instance inst_hmul {C : Type*} [Category C] [HasProduct C] : HMul C C C where
  hMul := prod

instance inst_mul {C : Type*} [Category C] [HasProduct C] : Mul C where
  mul := prod
```
 For example 
```lean
example {C : Type*} [Category C] [HasProduct C] (A B : C) : A*B = A*B := by rfl
```

Pairs of Morphisms
===

Pair only describes how to take the product of morphisms with the same domain.
The following method, which builds on `pair`, allows products of arbitary morphisms,
which will be useful in defining exponentials later.  
```lean
def prod_map {Xâ‚ Yâ‚ Xâ‚‚ Yâ‚‚ : C} (fâ‚ : Yâ‚ âŸ¶ Xâ‚) (fâ‚‚ : Yâ‚‚ âŸ¶ Xâ‚‚)
  : Yâ‚ * Yâ‚‚ âŸ¶ Xâ‚ * Xâ‚‚ := pair (Ï€â‚ â‰« fâ‚) (Ï€â‚‚ â‰« fâ‚‚)
```

Notation for Pairs of Morphisms
===

When `f` and `g` are morphisms, we want to write `f*g` for their prodict, so
we instantiate the notation class for `*` for morphisms as well.
```lean
notation "â€¹" fâ‚ ", " fâ‚‚ "â€º" => prod_map fâ‚ fâ‚‚

namespace Temp

variable (C : Type*) [Category C] [HasProduct C] (X Y : C) (f g : X âŸ¶ Y)
#check â€¹f,gâ€º
#check â€¹ Ï€â‚ â‰« f, g â‰« ğŸ™ Y â€º

end Temp
```

Helper Theorems
===

```lean
@[simp, reassoc]
theorem pair_id : pair (Ï€â‚ : X*Y âŸ¶ X) (Ï€â‚‚ : X*Y âŸ¶ Y) = ğŸ™ (X*Y) := by
    apply Eq.symm
    rw[â†Category.id_comp Ï€â‚, â†Category.id_comp Ï€â‚‚]
    apply pair_unique

@[simp, reassoc]
lemma comp_pair {h : W âŸ¶ X} {f : X âŸ¶ Y} {g : X âŸ¶ Z} :
  h â‰« pair f g = pair (h â‰« f) (h â‰« g) := by
  rw[pair_unique (h := h â‰« pair f g )]
  simp

@[simp]
theorem prod_to_pair {fâ‚ : Yâ‚ âŸ¶ Xâ‚} {fâ‚‚ : Yâ‚‚ âŸ¶ Xâ‚‚}
   : â€¹fâ‚,fâ‚‚â€º = pair (Ï€â‚ â‰« fâ‚) (Ï€â‚‚ â‰« fâ‚‚) := by rfl

@[simp]
theorem prod_map_comp {fâ‚ : X âŸ¶ Y} {fâ‚‚ : Y âŸ¶ Z} {gâ‚ : U âŸ¶ V} {gâ‚‚ : V âŸ¶ W}
  : â€¹ fâ‚ â‰« fâ‚‚, gâ‚ â‰« gâ‚‚ â€º = â€¹ fâ‚, gâ‚ â€º â‰« â€¹fâ‚‚, gâ‚‚â€º := by
  simp[â†Category.assoc] -- uses comp_pair and prod_to_pair

theorem pair_unique_simp {h : Y âŸ¶ prod Xâ‚ Xâ‚‚} : pair (h â‰« Ï€â‚) (h â‰« Ï€â‚‚) = h := by
  apply Eq.symm
  exact pair_unique

theorem pair_unique_simp2 {f : W âŸ¶ X * Y * Z}
  : pair (f â‰« Ï€â‚ â‰« Ï€â‚) (f â‰« Ï€â‚ â‰« Ï€â‚‚) = f â‰« Ï€â‚ := by
  simp[â†Category.assoc]
  apply pair_unique_simp

theorem pair_unique_simp3 {f : W âŸ¶ X * (Y * Z)}
  : pair (f â‰« Ï€â‚‚ â‰« Ï€â‚) (f â‰« Ï€â‚‚ â‰« Ï€â‚‚) = f â‰« Ï€â‚‚ := by
  simp[â†Category.assoc]
  apply pair_unique_simp

@[simp]
theorem hom_ext {A B : C} {f g : X âŸ¶ A * B} {hâ‚ : f â‰« Ï€â‚ = g â‰« Ï€â‚} {hâ‚‚ : f â‰« Ï€â‚‚ = g â‰« Ï€â‚‚}
  : f = g := by
    rw[pair_unique (h := f)]
    rw[pair_unique (h := g)]
    rw[hâ‚,hâ‚‚]

@[simp]
lemma prod_map_fst {f : A âŸ¶ X} {g : B âŸ¶ Y} :
    â€¹f,gâ€º â‰« Ï€â‚ = Ï€â‚ â‰« f := by simp

@[simp]
lemma prod_map_snd {f : A âŸ¶ X} {g : B âŸ¶ Y} :
    â€¹f,gâ€º â‰« Ï€â‚‚ = Ï€â‚‚ â‰« g := by simp
```

Projection Functors
===

```lean
def Fst : (C Ã— C) â¥¤ C where
  obj XY := XY.1
  map h := h.1

def Snd : (C Ã— C) â¥¤ C where
  obj XY := XY.2
  map h := h.2
```

The Product Bifunctor
===

```lean
def ProdBifunctor : (C Ã— C) â¥¤ C where
  obj XY := XY.1 * XY.2
  map { XY XY' } (h : XY âŸ¶ XY') := prod_map (C := C) h.1 h.2
  map_id := by
    intro XY
    simp[prod_map] -- uses pair_id
  map_comp := by
    intro X Y Z f g
    simp only [prod_comp, prod_map_comp]
```

Naturality
===

```lean
def Ï€â‚_nat : ProdBifunctor âŸ¶ Fst (C:=C) where
  app XY := Ï€â‚
  naturality {XY XY'} (h : XY âŸ¶ XY') := by
    simp[ProdBifunctor,Fst]

def Ï€â‚‚_nat : ProdBifunctor âŸ¶ Snd (C:=C) where
  app XY := Ï€â‚‚
  naturality {XY XY'} (h : XY âŸ¶ XY') := by
    simp[ProdBifunctor,Snd]
```

Proving Associativity
===
Yoneda says to show `X â‰… Y` we need to show `Z âŸ¶ X â‰… Z âŸ¶ Y`.

So to show `(A*B)*C â‰… A*(B*C)` it suffices to show
`Z âŸ¶ (A*B)*C â‰… Z âŸ¶ A*(B*C)`.

We'll do this by showing
- `Z âŸ¶ (A*B)*C â‰… (Z âŸ¶ A â¨¯ Z âŸ¶ B) Ã— (Z âŸ¶ C)`
- `Z âŸ¶ A*(B*C) â‰… (Z âŸ¶ A) â¨¯ (Z âŸ¶ B Ã— Z âŸ¶ C)`

Then via associativity of Ã— in the category Set,
`(Z âŸ¶ A â¨¯ Z âŸ¶ B) Ã— (Z âŸ¶ C) â‰… (Z âŸ¶ A) â¨¯ (Z âŸ¶ B Ã— Z âŸ¶ C)`

Transitivity allows us to conclude `Z âŸ¶ (A*B)*C â‰… Z âŸ¶ A*(B*C)`
after which we can applyt the Yoneda Lemma.



Step One
===

```lean
def t1 : (W âŸ¶ (X*Y)*Z) â‰ƒ ((W âŸ¶ X) Ã— (W âŸ¶ Y)) Ã— (W âŸ¶ Z) := {
      toFun f := ( ( f â‰« Ï€â‚ â‰« Ï€â‚ , f â‰« Ï€â‚ â‰« Ï€â‚‚ ), f â‰« Ï€â‚‚ ),
      invFun := fun âŸ¨ âŸ¨ f1, f2 âŸ©, f3 âŸ©  => pair (pair f1 f2) f3,
      left_inv := by
        intro f
        simp[pair_unique_simp2,pair_unique_simp],
      right_inv := by
        intro f
        simp[â†Category.assoc]
  }
```

Step Two
===

```lean
def t2 : (W âŸ¶ X*(Y*Z)) â‰ƒ (W âŸ¶ X) Ã— ((W âŸ¶ Y) Ã— (W âŸ¶ Z)) := {
      toFun f := ( f â‰« Ï€â‚, ( f â‰« Ï€â‚‚ â‰« Ï€â‚ , f â‰« Ï€â‚‚ â‰« Ï€â‚‚ ) ),
      invFun := fun âŸ¨ f1, âŸ¨ f2, f3 âŸ© âŸ©  => pair f1 (pair f2 f3),
      left_inv := by
        intro f
        simp[pair_unique_simp3,pair_unique_simp],
      right_inv := by
        intro f
        simp[â†Category.assoc]
  }
```

Step Three
===

```lean
def t3 : ((W âŸ¶ X) Ã— (W âŸ¶ Y)) Ã— (W âŸ¶ Z) â‰ƒ (W âŸ¶ X) Ã— ((W âŸ¶ Y) Ã— (W âŸ¶ Z)) := {
  toFun f := (f.1.1,(f.1.2,f.2)),
  invFun f := ((f.1,f.2.1),f.2.2),
  left_inv := by exact congrFun rfl,
  right_inv := by exact congrFun rfl
}
```

Naturality
===

```lean
def homAssocEquiv : (W âŸ¶ (X * Y) * Z) â‰ƒ (W âŸ¶ X * (Y * Z)) :=
  t1.trans (t3.trans t2.symm)

@[simp, reassoc]
lemma homAssocEquiv_natural (k : W' âŸ¶ W) (f : W âŸ¶ (X * Y) * Z)
  : homAssocEquiv (W:=W') (k â‰« f) = k â‰« homAssocEquiv (W:=W) f := by
    simp[homAssocEquiv,t1,t2,t3]

def homAssocNatIso {X Y Z : C} : yoneda.obj (((X * Y) * Z)) â‰… yoneda.obj (X * (Y * Z)) :=
  NatIso.ofComponents (fun W => {
    hom := fun f => (homAssocEquiv (W := Opposite.unop W)) f,
    inv := fun g => (homAssocEquiv (W := Opposite.unop W)).symm g,
    hom_inv_id := by
      funext f
      simp,
    inv_hom_id := by
      funext f
      simp
  })
```

The Resuting Associators
===

```lean
def assocIso {X Y Z : C} : ((X * Y) * Z) â‰… (X * (Y * Z)) :=
  (Yoneda.fullyFaithful).preimageIso homAssocNatIso

@[simp]
theorem prod_associator : (assocIso (X := X) (Y:=Y) (Z:=Z)).hom =
                          pair (Ï€â‚ â‰« Ï€â‚) (pair (Ï€â‚ â‰« Ï€â‚‚) Ï€â‚‚) := by
  simp[assocIso,homAssocNatIso,homAssocEquiv,t1,t2,t3,NatIso.ofComponents,Yoneda.fullyFaithful]

@[simp]
theorem prod_associator_inv : (assocIso (X := X) (Y:=Y) (Z:=Z)).inv =
                          pair (pair Ï€â‚ (Ï€â‚‚ â‰« Ï€â‚)) (Ï€â‚‚ â‰« Ï€â‚‚) := by
  simp[assocIso,homAssocNatIso,homAssocEquiv,t1,t2,t3,NatIso.ofComponents,Yoneda.fullyFaithful]

--hide
```

Conditions for a map to be the Identity
===

The next theorem describes when `f : X * Y âŸ¶ X * Y` is the identity on `X * Y`.

```lean
-- @[simp]
-- lemma prod_id_unique {f : X * Y âŸ¶ X * Y} {hâ‚ : f â‰« Ï€â‚ = Ï€â‚} {hâ‚‚ : f â‰« Ï€â‚‚ = Ï€â‚‚}
--   : f = ğŸ™ (X*Y) := by
--     rw[â†pair_id,â†hâ‚,â†hâ‚‚]
--     apply pair_unique
```

Simplifiers for Products
===

```lean
-- @[simp]
-- theorem prod_notation_to_pair {fâ‚ : Yâ‚ âŸ¶ Xâ‚} {fâ‚‚ : Yâ‚‚ âŸ¶ Xâ‚‚}
--    : â€¹fâ‚,fâ‚‚â€º = pair (Ï€â‚ â‰« fâ‚) (Ï€â‚‚ â‰« fâ‚‚) := by rfl
```

Theorems About Morphism Products
===

```lean
-- @[simp]
-- theorem prod_map_compf {fâ‚ : X âŸ¶ Y} {fâ‚‚ : Y âŸ¶ Z} {gâ‚ : U âŸ¶ V} {gâ‚‚ : V âŸ¶ W}
--   : â€¹ fâ‚ â‰« fâ‚‚, gâ‚ â‰« gâ‚‚ â€º = â€¹ fâ‚, gâ‚ â€º â‰« â€¹fâ‚‚, gâ‚‚â€º := by simp[â†Category.assoc]


-- theorem prod_map_unique {Z Xâ‚ Xâ‚‚ : C} {gâ‚ : Z âŸ¶ Xâ‚} {gâ‚‚ : Z âŸ¶ Xâ‚‚}
--   {h : Z âŸ¶ prod Xâ‚ Xâ‚‚} {hâ‚ : h â‰« Ï€â‚ = gâ‚} {hâ‚‚ : h â‰« Ï€â‚‚ = gâ‚‚} :
--   h = pair gâ‚ gâ‚‚ := by
--     rw[â†hâ‚,â†hâ‚‚]
--     exact pair_unique


-- @[simp]
-- theorem prod_map_id (X Y : C) :
--   (â€¹ğŸ™ X, ğŸ™ Yâ€º : (X * Y) âŸ¶ (X * Y)) = ğŸ™ (X * Y) := by
--   apply hom_ext
--   Â· simp
--   Â· simp

--unhide
```

Example Usage the Associators
===

```lean
lemma fst_fst : (Ï€â‚ : (X*Y)*Z âŸ¶ X*Y) â‰« (Ï€â‚ : X*Y âŸ¶ X) = assocIso.hom â‰« Ï€â‚ := by simp

lemma snd_snd : (Ï€â‚‚ : X*(Y*Z) âŸ¶ Y*Z) â‰« Ï€â‚‚ = assocIso.inv â‰« Ï€â‚‚ := by simp
```

Example: Graphs Have Products
===

Graphs have products called Tensor Products, which we can use to instantiate the `HasProduct` class.

<img src="https://docs.google.com/drawings/d/e/2PACX-1vS8m1ASMsZn0P7p6k0rOGj-8KKBhahoNL7SvrASBquIOwZdxX3_t_49JfFJ7WtowCD-AvSfSe1vkldt/pub?w=814&amp;h=368"
     width=50%>
</img>


```lean
def TensorProd (G H : Graph) : Graph := {
  V := G.V Ã— H.V,
  E := fun (u1,v1) (u2,v2) => G.E u1 u2 âˆ§ H.E v1 v2
}

--hide
namespace TensorProd
--unhide
```

Example: Tensor Product Properties
===

To form an instance of a `HasProduct` It will be convenient to have the following
properties defined as theorems, which state that products preserve edges.


```lean
theorem left {G H : Graph} :
  âˆ€ x y, (TensorProd G H).E x y â†’ G.E x.1 y.1 := by
  intro x y h
  exact h.left

theorem right {G H : Graph} :
  âˆ€ x y, (TensorProd G H).E x y â†’ H.E x.2 y.2 := by
  intro x y h
  exact h.right

--hide
end TensorProd
--unhide
```

Example: Graphs Have Products
===

Now we can instantiate the `HasProduct` class for graphs. 
```lean
instance Graph.inst_has_product : HasProduct Graph := {
  prod := TensorProd,
  Ï€â‚ := fun {Xâ‚ Xâ‚‚ : Graph} => âŸ¨ Prod.fst, TensorProd.left âŸ©,
  Ï€â‚‚ := fun {Xâ‚ Xâ‚‚ : Graph} => âŸ¨ Prod.snd, TensorProd.rightâŸ©,
  pair := fun {X Y Z} fâ‚ fâ‚‚ => âŸ¨ fun z => ( fâ‚.map z, fâ‚‚.map z ), by
      intro x y h
      exact âŸ¨ fâ‚.pe x y h, fâ‚‚.pe x y h âŸ©
    âŸ©
  pairâ‚ := by intros; rfl
  pairâ‚‚ := by intros; rfl
  pair_unique := by
    intro Xâ‚ Xâ‚‚ Y h
    exact rfl
}

--hide
end HasProduct
end LeanW26
--unhide
```

License
===

Copyright (C) 2025  Eric Klavins

This program is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.   

